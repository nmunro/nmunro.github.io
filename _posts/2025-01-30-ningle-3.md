---
layout: post
title:  "Ningle Tutorial 3: Static Files Middleware"
date:   2025-01-30 23:55:00 +0000
tags:   CommonLisp Lisp tutorial YouTube web dev
author: NMunro
---

## Contents

- [Part 1 (Hello World)](/2024/12/29/ningle-1.html)
- [Part 2 (Basic Templates)](/2024/12/30/ningle-2.html)
- Part 3 (Introduction to middleware and Static File management)
- [Part 4 (Forms)](/2025/02/28/ningle-4.html)
- [Part 5 (Environmental Variables)](/2025/03/31/ningle-5.html)
- [Part 6 (Database Connections)](/2025/04/30/ningle-6.html)
- [Part 7 (Envy Configuation Switching)](/2025/05/31/ningle-7.html)
- [Part 8 (Mounting Middleware)](/2025/06/29/ningle-8.html)
- [Part 9 (Authentication System)](/2025/07/31/ningle-9.html)
- [Part 10 (Email)](/2025/08/28/ningle-10.html)
- [Part 11 (Posting Tweets & Advanced Database Queries)](/2025/09/30/ningle-11.html)
- [Part 12 (Clean Up & Bug Fix)](/2025/10/29/ningle-12.html)
- [Part 13 (Adding Comments)](/2025/11/20/ningle-13.html)
- [Part 14 (Pagination, Part 1)](/2026/01/31/ningle-14.html)

## Introduction ##

Welcome back to this tutorial series, in this chapter we will be looking at the topic of `static files`, before we begin, we need to come to an understanding on just what `static files` are. Static files are files that do not need to be further processed by your application; images, videos, css, JavaScript files all generally do not need to be processed by your web applications, and thus can simply be served as is. Files that must be processed by your web application (like the templates from the previous tutorial) typically need further processing by your web application and thus are not static files, and could be called dynamic files (although this term isn't really used).

While developing an application there's often a requirement to de-couple these `static files` from the main application code, you might want to serve these separately in production and many web servers help you do this for performance reasons (in fact [NGINX](https://nginx.org/en/) is very good at this), however you might not need the scalability locally while you are working on your application, and so Ningle has a middleware module to serve these static files when you don't need a dedicated static file server.

Another, more practical consideration of serving static files is that if you don't have a way to serve these files for you, you would have to write a controller for every image, or css file in your application, this wont scale well at all, and you'll spend most of the time writing code to serve files rather than building your application. Static file management allows you to serve a directory of files and reference them by path, but you must set it up correctly in the first place.

Note: We will be using `djula`, as we did before, however as of 2025-01-15 this has not yet been merged into `quicklisp`, you may need to `clone` the latest `djula` from `github` into your `quicklisp/local-projects` directory to gain access to the latest version needed for this tutorial.

## Introducing Middleware ##

In reality Ningle deligates the responsibility of serving static files to the underlying `lack` package by way of the lack `middleware`. There are a number of different `lack` middleware modules available by default and throughout this tutorial we will look at most (if not all) of them.

In most web frameworks (Ningle included) middleware runs between the `request` being accepted and the code in your controller running. It is similar to a controller in that it has access to the `request` and `response` objects, and it _may_ pass its response onto the next middleware function or a controller, it depends on what the middleware function is written to do.

In the case of static files here, the end goal will be that a request for a file will come to your webserver, and the static middleware module will run before any controllers, and if the static resource is found, the middleware function will return a response and with our `not-found` method, if the url couldn't be found, our `not-found` method runs instead.

### Simple Middleware Example ###

To illustrate how this works in practice, we will write a piece of custom middleware that will add a new variable to the request environment, which we will then extract in a controller and display in a template, we'll use a number that gets incremented each time the middleware is run. In effect we will implement a hit counter in middleware!

Please note: This will not actually be used in the tutorial overall and serves only as a guide for how to write custom middleware generally, please follow this section to complete your understanding and feel free to include it (if you wish), but it will not be featured in the accompanying github code or used anywhere else in this tutorial. 

In our main application code we define an `app` objects, under this we will define a new variable to track our count.

{% highlight common_lisp %}
(defvar *app* (make-instance 'ningle:app))
(defvar *count* 0)
{% endhighlight %}

Now in order to take advantage of using middleware we must restructure how we built the `ningle` app, you may recall writing a `start` function that looked like the following.

{% highlight common_lisp %}
(defun start (&key (server :woo) (address "127.0.0.1") (port 8000))
    (clack:clackup
     *app*
     :server server
     :address address
     :port port))
{% endhighlight %}

We will need to edit this and introduce the idea of a `lack builder`. This is a way of building an application with more capabilities. Instead of simply passing our `*app*` object to the `clackup` function, we instead wrap our `*app*` object in the `lack builder` function which allows us to plug in middleware.

{% highlight common_lisp %}
(clack:clackup
     (lack.builder:builder *app*)
     :server server
     :address address
     :port port)
{% endhighlight %}

It may not be immediately obvious, but where previously the first argument to `clackup` was our `*app*` object, we instead call `lack.builder.builder` passing in `*app*`, it is in this `builder` call that we will hook in our middleware. Before we can do that however, we must write some middleware!

Above our start function I will write our middleware function:

{% highlight common_lisp %}
(defun hit-counter-middleware (app)
  (lambda (env)
    (setf (getf env :hit-counter) (incf *count*))
    (funcall app env)))
{% endhighlight %}

This is all it needs, we need to define a function that first accepts a ningle application object, and it returns a function (a `lambda` in this instance) that accepts the `env` (the `request` environment), because there may be a chain of middleware functions that potentially terminate with our `controller`, the `lambda` _must_ return the result of calling the next middleware function with the app and environment.

Within the body of the `lambda`, however, we are free to begin doing whatever we want!

In this example, we only do one thing, we add a new entry into the environment and assign it to be the incremented (`incf`) value of `*count*` with this line `(setf (getf env :hit-counter) (incf *count*))`.

We next must edit the `controller` to retrieve this stored value and render it into the template (which means we'll also need to edit the template).

Thankfully editing our `controller` is easy, we need only add a new `keyword` argument to the `render-template*` function.

{% highlight common_lisp %}
(setf (ningle:route *app* "/")
      (lambda (params)
        (let ((user  (list :username "NMunro"))
              (posts (list (list :author (list :username "Bob")  :content "Experimenting with Dylan" :created-at "2025-01-24 @ 13:34")
                           (list :author (list :username "Jane") :content "Wrote in my diary today" :created-at "2025-01-24 @ 13:23"))))
          (djula:render-template* "index.html" nil :title "Home"
                                                   :user user
                                                   :posts posts
                                                   :hit-counter (getf (lack/request:request-env ningle:*request*) :hit-counter)))))
{% endhighlight %}

The only addition is the `:hit counter (getf (lack/request:request-env ningle:*request*) :hit-counter)` line. This will retrieve the `:hit-counter` value from the `request` environment.

In our `index.html` template, in the `div` with the `class="container"`, we will add the following:

{% highlight html %}
    <div class="row">
        <div class="col-12">
            <h4>Hits</h4>
            <p>{% raw %}{{ hit-counter }}{% endraw %}</p>
        </div>
    </div>
{% endhighlight %}

The last thing we must do is return to the `lack.builder` section of our `start` function and hook the middleware into the app.

{% highlight common_lisp %}
(lack.builder:builder #'hit-counter-middleware *app*)
{% endhighlight %}

It _must_ be included before `*app*` as the `hit-counter-middleware` will be wrapping our application and run before anything in our app does. As this tutorial (and your own applications) grow, this line and the different middleware modules will change as requirements do.

If you save and load the project, you should see that there is a `div` in your template that updates a count every time the page is refreshed. At this point you may notice that the counter is incremented by 2 each time, this is not a mistake, this is because your web browser will request the page itself, and a `favicon.ico` file (and hit the `not-found` controller). 

For clarity here is the edited `main.lisp` file:

{% highlight common_lisp %}
(defpackage ningle-tutorial-project
  (:use :cl)
  (:export #:start
           #:stop))

(in-package ningle-tutorial-project)

(defvar *app* (make-instance 'ningle:app))
(defvar *count* 0)

(setf (ningle:route *app* "/")
      (lambda (params)
        (let ((user  (list :username "NMunro"))
              (posts (list (list :author (list :username "Bob")  :content "Experimenting with Dylan" :created-at "2025-01-24 @ 13:34")
                           (list :author (list :username "Jane") :content "Wrote in my diary today" :created-at "2025-01-24 @ 13:23"))))
          (djula:render-template* "index.html" nil :title "Home"
                                                   :user user
                                                   :posts posts
                                                   :hit-counter (getf (lack/request:request-env ningle:*request*) :hit-counter)))))

(defmethod ningle:not-found ((app ningle:<app>))
    (declare (ignore app))
    (setf (lack.response:response-status ningle:*response*) 404)
    (djula:render-template* "error.html" nil :error "Not Found"))

(defun hit-counter-middleware (app)
  (lambda (env)
    (setf (getf env :hit-counter) (incf *count*))
    (funcall app env)))

(defun start (&key (server :woo) (address "127.0.0.1") (port 8000))
    (djula:add-template-directory (asdf:system-relative-pathname :ningle-tutorial-project "src/templates/"))
    (clack:clackup
        (lack.builder:builder #'hit-counter-middleware *app*)
     :server server
     :address address
     :port port))

(defun stop (instance)
    (clack:stop instance))
{% endhighlight %}

Understanding how to write custom middleware is very important, and I hope that this has served as a good foundation, however, as mentioned at the beginning of this section we will not be using this piece of custom middleware in our application. You are free to include it if you wish, but it will not feature in the companion code in github.

### Aceesslog Middleware ###

Now that we have discussed what middleware is, work it does, how it works, and how to implement it, we will look at some of the middleware modules included in `lack` which `ningle` therefore has access to.

We will start with what is known as `accesslog` middleware, it's a very simple piece of middleware that just logs requests as they come in.

As we did in the previous section, we must adjust the `lack.builder` line, however, this time we do not need to write any function, the middleware that comes with `lack` uses some simplified syntax.

{% highlight common_lisp %}
(defun start (&key (server :woo) (address "127.0.0.1") (port 8000))
    (djula:add-template-directory (asdf:system-relative-pathname :ningle-tutorial-project "src/templates/"))
    (clack:clackup
        (lack.builder:builder :accesslog *app*)
     :server server
     :address address
     :port port))
{% endhighlight %}

If you recompile and run your application, and view the terminal output, you will see information about the incoming requests to the web server.

This is a simple example, but it highlights an important distinction that the bundled `lack` middleware isn't a reference to a `function`, it's a `keyword`, as we will see in the next section, they can be a little more complicated than _just_ a `keyword`, but this particular piece of middleware, it is just a `keyword` to turn it on. Other pieces of middleware may be a `list` that include configuration options, if needed.

### Static Files Middleware ###

What we would like to do, when we write our templates is to be able to tell our template that a file is a static file and must be served from the static location. We will need to use a special `djula` tag to inform our templates that a file is a static file, which may seem a little counter intuitive, however, if for some reason we need to change where static files are served from (for example we may initially host them locally, but then switch to s3 or cloudflare or something), we'd have to go through all our templates changing the url, whereas using static file middleware, we'd set up a base once, and if we need to change it, we change it in one place and then our templates wouldn't need to change at all.

While this sounds like a lot of work, remarkably, it isn't!

There's only really three steps to setting up static file handling in Ningle!

As we are using `djula` (and a reminder `quicklisp` may not yet have the latest version of `djula`, you may need to use git to clone it into your `quicklisp/local-projects`), we must configure `djula` to be aware of where our static files will be mounted. So, just as we added a template directory, we must also add a static directory, in our example this is in the `start` function:

{% highlight common_lisp %}
(djula:add-template-directory (asdf:system-relative-pathname :ningle-tutorial-project "src/templates/"))
(djula:set-static-url "/public/")
{% endhighlight %}

This second line is the one we have added, when we use the `static` `tag` later on, it will know to use "/public/" as our static path.

NOTE: Be mindful to ensure there's a trailing slash when calling `set-static-url`!

The second thing we must do is hook into the `lack` static middleware.

{% highlight common_lisp %}
(lack.builder:builder :accesslog
                      (:static
                       :root (asdf:system-relative-pathname :ningle-tutorial-project "src/static/")
                       :path "/public/")
                      *app*)
{% endhighlight %}

Mentioned previously, some middleware setup will be lists, in this instance, a list where the first item is a `keyword` naming the `lack` middleware module to use (this will be a common pattern with other `lack` middleware) and then any arguments that the middleware module uses. In this case, we need to define where on our host file system we will be storing our static files, this is the `:root` argument and we specify that relative to our project, static files will be stored in `/src/static` and we need to have these mounted on a `path` which is exactly what the `:path` argument does, we will hide the physical location of our static files (good security) and state that they're available behind `"/public/"`.

For clarity, this is what the start function should look like:

{% highlight common_lisp %}
(defun start (&key (server :woo) (address "127.0.0.1") (port 8000))
    (djula:add-template-directory (asdf:system-relative-pathname :ningle-tutorial-project "src/templates/"))
    (djula:set-static-url "/public/")
    (clack:clackup
      (lack.builder:builder :accesslog
                            (:static
                             :root (asdf:system-relative-pathname :ningle-tutorial-project "src/static/")
                             :path "/public/")
                            *app*)
     :server server
     :address address
     :port port))
{% endhighlight %}

The final thing we need to do is, in our templates, use the static tag to load a given static file. In the `base.html` file, you might want to display an image. You can use whatever image you like, but if you want to use the one I've created, you can use [this](https://raw.githubusercontent.com/nmunro/ningle-tutorial-project/f1295e0779742d442c4b24871164cc2d666d74b4/src/static/images/logo.jpg).

You should put this file (or the image of your choice) in the `src/static/images/` directory (and create it, if you have not), I have called the image `logo.jpg` and have stored it in `src/static/logo.jpg`. This will exposed it as `/public/images/logo.jpg` and from here we can place these into our templates. 

{% highlight html %}
<img src='{% raw %}{% static "images/logo.jpg" %}{% endraw %}' alt='Logo'>
{% endhighlight %}

If you save, reload, and view this project in your web browser, you should see the image rendered as you might expect. Inspecting the page you will see that the `src` attribute will be `src="/public/images/logo.jpg"`. The image is being served without writing having to write a controller, and is served from the root you defined.

### Tidying Up ###

Now that we have the ability to serve images, css etc, we might want to take this time to writing some css (although I personally hate writing CSS), and making the site look good. Although it is beyond this tutorial to teach `bootstrap` or other `css` frameworks (although I will use bootstrap), I will be using bootstrap to make my site look a little nicer, you can refer to the [github code](https://github.com/nmunro/ningle-tutorial-project/releases/tag/tutorial-3) to see exactly what I have done regarding `frontend` styling.

There is something I will do to help our application look a little better...

I will create a nicer looking error page that will take advantage of our new `staticfiles` middleware, so the contents of `src/templates/error.html` will be:

    {% raw %}{% extends "base.html" %}{% endraw %}

    {% raw %}{% block content %}{% endraw %}
        <div class="container">
            <div class="row">
                <div class="col-12">
                    <h1>{% raw %}{{ error }}{% endraw %}</h1>
                    <img src="{% raw %}{% static "images/lua.jpg" %}{% endraw %}" alt="A picture of a dog looking sad and confused" class="error-404">
                </div>
            </div>
        </div>
    {% raw %}{% endblock %}{% endraw %}
    
I will save [this photo](https://raw.githubusercontent.com/nmunro/ningle-tutorial-project/7588679c160aedc2b7e72e6a00665d7f72ce6c34/src/static/images/lua.jpg) to `src/static/images/lua.jpg`.

And in the `main.lisp` file, I will modify the `not-found` method:

{% highlight common_lisp %}
(defmethod ningle:not-found ((app ningle:<app>))
    (declare (ignore app))
    (setf (lack.response:response-status ningle:*response*) 404)
    (djula:render-template* "error.html" nil :error "Not Found"))
{% endhighlight %}

I have also edited the `controller` for the index page:

{% highlight common_lisp %}
(setf (ningle:route *app* "/")
      (lambda (params)
        (let ((user  (list :username "NMunro"))
              (posts (list (list :author (list :username "Bob")  :content "Experimenting with Dylan" :created-at "2025-01-24 @ 13:34")
                           (list :author (list :username "Jane") :content "Wrote in my diary today" :created-at "2025-01-24 @ 13:23"))))
          (djula:render-template* "index.html" nil :title "Home"
                                                   :user user
                                                   :posts posts))))
{% endhighlight %}

In my frontend I have edited the html to include a `created-at` attribute to the posts and included it as we did before with the post author and content:

    <h5 class="card-title">{% raw %}{{ post.author.username }}{% endraw %}</h5>
    <p class="card-text">{% raw %}{{ post.content }}{% endraw %}</p>
    <p class="text-muted small">Posted on: {% raw %}{{ post.created-at }}{% endraw %}</p>
    
The exact styling I leave up to you, but I wanted to be clear that there is a _small_ content change to the html.

## Conclusion ##

To recap, after working your way though this tutorial you should be able to:

- Describe what static files are.
- Describe what application middleware is.
- Explain why it is advantagous to handle static files differently.
- Explain how middleware works.
- Create and use simple middleware functions.
- Incorporate lack static middleware into your application.
- Incorporate djula static tags in your html templates to serve static content.

## Github ##

The link for this tutorial is available [here](https://github.com/nmunro/ningle-tutorial-project/releases/tag/tutorial-3).

## Resources ##

- [Clack](https://github.com/fukamachi/clack/)
- [class](https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/class)
- [div](https://developer.mozilla.org/en-US/docs/Web/HTML/Element/div)
- [djula](https://github.com/mmontone/djula)
- [djula documentation](https://mmontone.github.io/djula/djula/)
- [favicon](https://developer.mozilla.org/en-US/docs/Glossary/Favicon)
- [function](http://clhs.lisp.se/Body/02_dhb.htm)
- [getf](http://clhs.lisp.se/Body/f_getf.htm)
- [incf](http://clhs.lisp.se/Body/m_incf_.htm)
- [keyword](http://clhs.lisp.se/Body/t_kwd.htm)
- [quicklisp](https://www.quicklisp.org/beta/)
- [setf](http://clhs.lisp.se/Body/m_setf_.htm)
- [Lack](https://github.com/fukamachi/lack/)
- [ningle](https://github.com/fukamachi/ningle)
- [format](http://clhs.lisp.se/Body/f_format.htm)
- [lambda](http://clhs.lisp.se/Body/m_lambda.htm)
- [middleware](https://en.wikipedia.org/wiki/Middleware)
- [NGINX](https://nginx.org/en/)
